Maintenance:
	Title:		general partner related Question;;
	Filename:	selectGeneralPartners.mlm;;
	Version:	1.0;;
	Institution:	Indiana University School of Medicine;;
	Author:		Jun Wang;;
	Specialist:	Software Engineer;;
	Date:		2010-03-18T16:12:57-0500;;
	Validation:	research;;

Library:
	Purpose:	select general partners.  ,,multiSelect ,,generalPartnerOptions;;
	Explanation:	;;
	Keywords:	;;
	Citations:	;;
	Links:		;;

Knowledge:
	Type:		data_driven;;

Data:

mode:=read {mode from Parameters};
Box1:=read {input1value from Parameters}; /* dropdown list */
Box2:=read {input2value from Parameters}; /* text entry */

If (mode = PRODUCE) then
 interactedWithGeneralPartners := read Last {Interacted with someone that subject is interested in since last scheduled prompt} Where it Occurred Within Past 1 Days;
 partnerList := read {general previous partner list};
 generalPartnerInput := read Last {General partner} Where it Occurred Within Past 1 Days; /* see if this question answered */
endif
;;

Priority:		268;;
	Evoke:		;;

Logic:
If (mode = PRODUCE) then

If NOT (generalPartnerInput = NULL) then conclude False; /* don't show if partners already chosen */
If (interactedWithGeneralPartners = Yes) then conclude True;
conclude False;

endif

If (mode = CONSUME) then

/* If partners chosen from dropdown, store them under general partner. This is the active
partner list for this survey session */
If NOT (Box1 = NULL) then
 CALL storeObsMultipleAnswers With "General partner", "Box1";
endif;

/* If new partners entered in text box, store under previous partner list and 
store them under general partner. This is the active partner list for this survey session */
If NOT (Box2 = NULL) then
 CALL storeObsMultipleAnswers With "general previous partner list", "Box2";
 CALL storeObsMultipleAnswers With "General partner", "Box2";
endif;

/* If there is no input, indicate that the question was skipped */
If (Box1 = NULL) AND (Box2 = NULL) then
  CALL storeObs With "General partner", "SKIPPED";
endif;

endif
;;

Action:
write ("Select/Enter names of <b> up to Three partners (not have sex with) </b> talked to, interested in since last scheduled prompt:");
write ("translate@Spanish");
write ("dropdown_entry@input_type_1");
write ("|| partnerList ||@input_list_1");
write ("text_entry@input_type_2");
;;

Age_Min: 14 years;;
Age_Max: 16 years;;

end:
